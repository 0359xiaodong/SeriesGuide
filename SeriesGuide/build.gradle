apply plugin: 'android'
apply plugin: 'crashlytics'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    productFlavors {
        free {
            applicationId "com.battlelancer.seriesguide"
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName
        }
        internal {
            applicationId "com.battlelancer.seriesguide.beta"
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName + "-internal"
        }
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile project(':libraries:androidutils')
    compile project(':libraries:pagerslidingtabstrip')
    compile project(':libraries:stickygridheaders')
    compile rootProject.ext.libraries.okhttp
    compile rootProject.ext.libraries.supportv4
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.github.chrisbanes.photoview:library:1.2.2'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.+'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'com.jakewharton.timber:timber:2.2.+'
    compile 'com.readystatesoftware.systembartint:systembartint:+'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile 'com.squareup.retrofit:retrofit:1.4.1'
    compile 'com.squareup:tape:1.1.1'
    compile 'com.uwetrottmann:tmdb-java:0.4.+'
    compile 'com.uwetrottmann:trakt-java:3.1.+'
    compile 'de.greenrobot:eventbus:2.2.0'

    // OAuth 2.0
    compile 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:0.31'

    // Google Play Services
    compile 'com.google.android.gms:play-services:4.4.52'

    // libs folder
    compile fileTree(dir: 'libs', include: '*.jar')

    // Crashlytics
    compile 'com.crashlytics.android:crashlytics:1.+'
}

// get signing config from gradle.properties file
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
