/*
 * Copyright 2014 Uwe Trottmann
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

jar.baseName = 'seriesguide-api'
version = '1.0'

// load properties file
def Properties props = new Properties()
props.load(new FileInputStream(file('../local.properties')))
// extract Android SDK dir
def android = [
        sdk: props["sdk.dir"],
        target: 'android-19'
]

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = jar.baseName
    from sourceSets.main.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    baseName = jar.baseName
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile files("${android.sdk}/platforms/${android.target}/android.jar")
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        api(MavenPublication) {
            groupId 'com.uwetrottmann.seriesguide'
            artifactId 'seriesguide-api'
            // version from gradle version above

            pom.withXml {
                asNode().appendNode('name', 'SeriesGuide API')
                asNode().appendNode('description', 'Extension API for SeriesGuide to provide custom actions on media items')
                asNode().appendNode('url', 'http://api.seriesgui.de')
                asNode().appendNode('licenses')
                        .appendNode('license')
                        .appendNode('name', 'The Apache Software License, Version 2.0')
                        .parent().appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                asNode().appendNode('developers')
                        .appendNode('developer')
                        .appendNode('name', 'Uwe Trottmann')
                        .parent().appendNode('email', 'uwe.trottmann@gmail.com')
                asNode().appendNode('scm')
                        .appendNode('connection', 'scm:git:git://github.com/UweTrottmann/SeriesGuide.git')
                        .parent().appendNode('developerConnection', 'scm:git:git@github.com:UweTrottmann/SeriesGuide.git')
                        .parent().appendNode('url', 'https://github.com/UweTrottmann/SeriesGuide.git')
            }

            from components.java
        }
    }
}